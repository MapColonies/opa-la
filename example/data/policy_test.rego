package http.authz

private_key_1 := {
	"kty": "RSA",
	"n": "sblCdtKBNbpuVmLWs5N6sIS3TJdccFno3XdRDg_CbZtNgLb9ZGk2YxPlXqdwFW7mv3R_W7zrhezk_XOa8n4Phm3PUmyrg6ha7dUwS7ZvzK8dxHyDOBySWRrS0Py_7iFyDxE8Oeb1gRQ84NsnhH41ln3jcS35nrqj0IeSpFzG9ZUErvhTjIU3x-rUNIYLDeOQp39wY-Y39S6tZ8BGecPtShBOnWoSgPFVLcMvK1WFW7xRdYP-kcof2xjBIejAYDOr-utazDclVh5Zl5fzeiUWo7HQkbvX-g-DMth1tfPGeyzQJhX6HAyr9mPfI-c0iE2gnxH4dWmsEWpolw3Ru-jeSQ",
	"e": "AQAB",
	"d": "O6pIh_-v5UvLkzDnh64xeGV17D68h_OHXCKcka30xjtnT5kfhMBVw_l__cEUvf1Xdls0DqZkI7geQIC5OkPcDnN7MN7OzgqNTF9QMXr0GbGIuthabVIaR9PbhtHz1Yxg8HIG2CGTtlwG_C5XHAILF5T38J1QultKd82sC4YiPXvUlVFlfZF7BezQxPEYRc_uveRTdQznKBR82Q1vkjM6xelwBMOD_Xq_0ef3ql6-nPrwCnUck7kzliDwe9qqHkmlKhPBuBWIQuB9IGOrxQCJZYRtCJbspTxPRTaoyBV_czp-az7xWCzai_DEJullqdzhFD6KA8X-dZf8XTjjbEfp6Q",
	"p": "3tIP-3k443tS_qvS5kdTpaKxJoD182fJdWlbTjuFSLKgMzRVdr8PiaSOG3RD4dzfufjAfP3AFko4nQnC59kuPdRbNJwD7MNzAuvkquUkRq7OB0g_Fj5pC3PcKaRU57mpsCc6thdIXsOKzvCZgXM8IxncxI1G0Su-Um8bHgr8M58",
	"q": "zDAZqxf8zoX69XbOZxYZRywk3OTpkD18_ZEK4cSZkrrOij79SFIU1e4oYFFrCGxj2hhKgp2hGE3TqSty9wYssbVdNlERICJPX4DZj9Qt5_ijb8bG2_r7VG1CjRn-qEtSSLjdrdEsbdx_qBFzNH-wHXguQ93loJvsJqoEJboN5Rc",
	"dp": "X2e4OiecaSKb1bmCcuEleq1fhqn_JXpR8zjqRGQN3KPHHkWRNmf2YiwYQL8WdbYaAUn2OU55GdIrzWmpj5YZS5YKe0s2DwFc1GpmnZnBX5ZnVwzjHkYYujOgmB_pztJbSrZxWBg9_31giNzSDXBm1myzb2FCajt9oVQ7WzC-7ec",
	"dq": "Ohf9VZZvkPrBmhEBTIcXDg7bNhXS9fzokOQxamabIwoPNXoSaf8genVV-4FuqGjeR_DdUigy601JSTpZbTrOgIkPSiLqcnAQkSWBwNAnd3ZgWa7-aRwRYcXsR2T79Tno4VPnjkqTD3bKwzCjzG3_14KX9yss5_M0zxNUNr-msGU",
	"qi": "aX33ijcsC0itjrEuYzv9F6yiLHvToDu6eF5JdZkNIzCeAX9_jIN8G75d4j4G0xPjwCRRnT3RgXExNLJArNSv73zhxWvA6eQc_LL4wyaVjWU9UDg1yec6NmEow2QEHGr5W1vlu5bRk7QZYEKmhjLO3f4icIkyEoprHYt-tMZ2qtE",
	"alg": "RS256",
	"kid": "opa_test_key_1",
}

public_key_1 := {
	"kty": "RSA",
	"n": "sblCdtKBNbpuVmLWs5N6sIS3TJdccFno3XdRDg_CbZtNgLb9ZGk2YxPlXqdwFW7mv3R_W7zrhezk_XOa8n4Phm3PUmyrg6ha7dUwS7ZvzK8dxHyDOBySWRrS0Py_7iFyDxE8Oeb1gRQ84NsnhH41ln3jcS35nrqj0IeSpFzG9ZUErvhTjIU3x-rUNIYLDeOQp39wY-Y39S6tZ8BGecPtShBOnWoSgPFVLcMvK1WFW7xRdYP-kcof2xjBIejAYDOr-utazDclVh5Zl5fzeiUWo7HQkbvX-g-DMth1tfPGeyzQJhX6HAyr9mPfI-c0iE2gnxH4dWmsEWpolw3Ru-jeSQ",
	"e": "AQAB",
	"alg": "RS256",
	"kid": "opa_test_key_1",
}

private_key_2 := {
	"kty": "RSA",
	"n": "7yGzPxFIxM2sxWojt-jYlVyoO_oeFya647mFMq_5ZEhrJypu0iN5I2iDdjEx6k7osXk5-qNgfLvhx8OPUGC043WKiPRQicjgRcR47NNavY2eTmFOwe_a5ey0fCmAzLeA3FHNAmzOBlZ4U6taJCpAR5_vaPwkF-mcaOCm9eEO4NkKjFV05yVG6wh9GcCunMmMe6Bs6RHPmVVtl3xfbYBpYci6sIOZIsJujXCklwh6XvVQDD0hJUyTX-GPywAxU9nUDaBbQvQoljOf-2TE3G_Mw0V1MkZLzlXsy8-jBa7eZ6XI9MjD-CnlOI9t7qWmKGRcgX5VsAZmyYJQ995YUoaq9Q",
	"e": "AQAB",
	"d": "eCXWibmFdlxgkk_h4mV7bJBBduEUfU1YWVK9Odpw05IPRH5tb-ei1ceNRbUx_yfmgkUGfIfZ0tITfusi9p-gQyirkmQukwv6oxM0LsmCrfqZr4f0qjx5H0zu4fN4Y2NPu2LePnmsikKn8mIFyGXyJgqVn4oQGHOnwoodWSza7N0nWnQlLGE3FTVNq4-tHvzrf4P4BXAMvzAeDXUkoCAv9-1i-7DomZM94y1r2w-64pF1m9XbUgJqT3v6uZqN-JJHOkpoNX5qcLnLIOHL_MFS8Fqw-wVowovLAI57yzchZT0x_0WmuW1KbuV9RWqL2NtuP5LLklcSO6P7s53G26UYAQ",
	"p": "_axgheGEleyetH8ONoBTjJb8ngoZo-zGmRySi6k-_r8cnf4krfAgeTT86rvuCNEBFaBIB0QxZfNYiD3u72FkAqSoMvfU7z9cM4K2vq-g171CkNibrWas3GbD8y9o6l6kJF_73GcjQbX8wGG-x1KDtgNxTEhm3u7eo-E8B_WXuXE",
	"q": "8VMt6IyrHQqB5LXV2oQgVgAigWz2iVj9j5XhubpERt8lzsGkDDRoDsvM7EsXSZtK3cUuo6UqoXeuhEX_iPN0E_ycVhIZQO8QrTJr_vi1dYIMlZC5BNez578oE6YcTt65UITxberuOk206msBmuQAcIt6KrO7HDu93aq2Qntw58U",
	"dp": "eVWhGYiD9X4nbygysSjyTZXOoP0txHW1jHjZM4oxgoIA-yUbgEMSFUeeDdzz_y2ROjnfGfKpOh2KZChTiBZqUsVVWoOmSwVgYZcNN-ojoe150HR7ChbJaeaRpVkw8qFwG6H5gzOl7oFQsuhN62Lxcvb0k0syQUG6JmLOZvQ2rVE",
	"dq": "pIFiQytZHfPitgjqXgpBjL6MxxODP_-E4dN3S0dZccJ-IGJKNPOol6V_7PgHSOrubx0SfQWDeQx_z7Vzy2TCFTnXTKkLxALzqE0951KdSBw9_dro9Q8hmXHqnPJYS79yLhxiA97pRJ9iG4aUUoToc8_wmJlqjIWBGVFc6xcA0U0",
	"qi": "LcC11Cg63JXxwW9tQvb9ENCil6U0nOOqv2BydMHCYuBZb0WlO6j1zUTVL8_SnPPQewW8Q9i9fKg8xVeiPRgNiBue-JSTq6XCAA0wDuEsUdBGq2G_NLpb0OubUsr7N_slJVAbt12L011FZq2wa4n1nUw_xfyQGQT7sWRFkUYcPJA",
	"alg": "RS256",
	"kid": "opa_test_key_2",
}

chrome_agent := "Mozilla/5.0 (X11; Linux x86_64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/108.0.0.0 Safari/537.36"

postman_agent := "PostmanRuntime/7.32.2"

qgis_agent := "Mozilla/5.0 QGIS/34400/Ubuntu 22.04.5 LTS"

users := {
		"avi": {
		"allowNoBrowser": false,
		"allowNoOrigin": false,
		"domains": ["avi"],
		"origins": ["https://avi.com"],
		}, 
		"itzik": {
			"allowNoBrowser": false,
			"allowNoOrigin": false,
			"domains": ["avi"],
			"origins": ["*.itzik.maps.com"],
		}, 
		"zvika": {
			"allowNoBrowser": false,
			"allowNoOrigin": false,
			"domains": ["avi"],
			"origins": ["https://google.com", "*.zvika.maps.com"],
		}
}

generate_token(key, payload) := token if {
	newPayload := json.patch(payload, [{"op": "add", "path": "iss", "value": "mapcolonies-token-cli"}])
	token := io.jwt.encode_sign(
		{
			"typ": "JWT",
			"alg": "RS256",
		},
		newPayload,
		key,
	)
}

test_allowed_header if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
    with data.users as users

	res.allowed
}

test_allowed_query if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "User-Agent": chrome_agent}, "query": {"token": token}}
		with data.keys as public_key_1
    with data.users as users

	res.allowed
}

test_allowed_no_origin if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
    with data.users as users
    with data.users.avi.allowNoOrigin as true

	res.allowed
}

test_allowed_backend_origin if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": postman_agent}}
		with data.keys as public_key_1
		with data.users as users
    with data.users.avi.allowNoBrowser as true

	res.allowed
}

test_allowed_same_origin if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": postman_agent, "Sec-Fetch-Site": "same-origin"}}
		with data.keys as public_key_1
		with data.users as users

	res.allowed
}

test_deny_no_token if {
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "no token supplied")
}

test_deny_malformed_token if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "X-Api-Key": substring(token, 0, 40), "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "token not valid")
}

test_deny_token_wrong_key if {
	token := generate_token(private_key_2, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "token not valid")
}

test_deny_user_not_found if {
	token := generate_token(private_key_1, {"sub": "nx_user"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "user details missing")
}

test_deny_token_wrong_domain if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"itzik", "headers": {"Origin": "https://avi.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "domain check failed")
}

test_deny_token_domain_missing if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"headers": {"Origin": "https://avi.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
  contains(res.reason, "domain missing")
}

test_deny_wrong_origin if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"Origin": "https://avi2.com", "X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_deny_missing_origin if {
	token := generate_token(private_key_1, {"sub": "avi"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_allow_wildcard_origin if {
	token := generate_token(private_key_1, {"sub": "itzik"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent, "Origin": "https://meow.itzik.maps.com"}}
		with data.keys as public_key_1
		with data.users as users

	res.allowed
}

test_deny_wildcard_origin if {
	token := generate_token(private_key_1, {"sub": "itzik"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent, "Origin": "https://meow.avi.maps.com"}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_allow_specific_origin_for_wildcard_user if {
	token := generate_token(private_key_1, {"sub": "zvika"})
	res := decision with input as {"domain":"avi", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent, "Origin": "https://google.com"}}
		with data.keys as public_key_1
		with data.users as users

	res.allowed
}

test_deny_specific_origin_for_wildcard_user if {
	token := generate_token(private_key_1, {"sub": "zvika"})
	res := decision with input as {"domain":"avi", "headers": { "User-Agent": chrome_agent, "Origin": "https://ggl.com"}, "query": {"token": token}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_deny_not_matching_glob_pattern_for_wildcard_origin_user if {
	token := generate_token(private_key_1, {"sub": "zvika"})
	res := decision with input as {"domain":"avi", "headers": { "User-Agent": chrome_agent, "Origin": "https://avi.bla.zvika.maps.com"}, "query": {"token": token}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_allow_c2b_connection_from_qgis if {
	token := generate_token(private_key_1, {"sub": "c2b"})
	res := decision with input as {"domain":"raster", "headers": {"X-Api-Key": token, "User-Agent": qgis_agent}}
		with data.keys as public_key_1
		with data.users as users

	res.allowed
}

test_deny_c2b_connection_if_token_expired if {
	token := generate_token(private_key_1, {"sub": "c2b", "exp": time.now_ns() / 1000000000 - 60})  # 1 minute ago
	res := decision with input as {"domain":"raster", "headers": {"X-Api-Key": token, "User-Agent": qgis_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}

test_deny_c2b_connection_from_browser if {
	token := generate_token(private_key_1, {"sub": "c2b"})
	res := decision with input as {"domain":"raster", "headers": {"X-Api-Key": token, "User-Agent": chrome_agent}}
		with data.keys as public_key_1
		with data.users as users

	not res.allowed
}
